; ***************************************************************************************
; ***************************************************************************************
;
;		Name : 		memory.asm
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		31st October 2021
;		Purpose :	Memory operators
;
; ***************************************************************************************
; ***************************************************************************************

;; [MACRO]	!
		ld 		(hl),e
		inc 	hl
		ld 		(hl),d
		dec 	hl
;; [END]

; ***************************************************************************************

;; [MACRO]	@
		ld 		a,(hl)
		inc 	hl
		ld		h,(hl)
		ld		l,a
;; [END]

; ***************************************************************************************

;; [CALL]	+!
		ld 		a,(hl)
		add 	a,e
		ld 		(hl),a
		inc 	hl
		ld 		a,(hl)
		adc 	a,d
		ld 		(hl),a
		dec 	hl
		ret
;; [END]

; ***************************************************************************************

;; [MACRO]	c!
		ld 		(hl),e
;; [END]

; ***************************************************************************************

;; [MACRO]	c@
		ld 		l,(hl)
		ld 		h,0
;; [END]

; ***************************************************************************************

;; [CALL]	p@
		push 	bc
		ld		b,h
		ld 		c,l
		in 		l,(c)
		ld 		h,0
		pop 	bc
		ret
;; [END]

; ***************************************************************************************

;; [CALL] 	p!
		push 	bc
		push 	hl
		ld 		a,e
		ld		b,h
		ld 		c,l
		out 	(c),a
		pop 	hl
		pop 	bc
		ret
;; [END]
	
	