              	; --------------------------------------
              	; zasm: assemble "_test.asm"
              	; date: 2021-11-01 09:48:41
              	; --------------------------------------


              	
0202:         			org 	$202
              	
              	;
              	;	 	Defining SPRLowSprite sets the lowest UDG used by sprites. This is a constant
              	; 		so if this is $A0, then $00-$9F are background, and $A0-$FF are used for sprites.
              	;
0080:         	SPRLowSprite = $80 								
              	
0202: C30009  			jp 		start
              			.include "xsprite.asm"
              	; *********************************************************************************************
              	; *********************************************************************************************
              	;
              	;		Name:		xsprite.asm
              	;		Purpose:	XOR Sprite Drawer
              	;		Created:	29th October 2021
              	;		Author:		Paul Robson (paul@robsons.org.uk)
              	;
              	; *********************************************************************************************
              	; *********************************************************************************************
              	
              	; *********************************************************************************************
              	;
              	;		This is an XOR drawing sprite system. This is useful because the draw is self
              	;		cancelling, and this means you don't have to repaint multiple sprites to restore
              	; 		the display state. The downside is that it doesn't work well on collisions or 
              	; 		especially backgrounds.
              	;
              	;		The original design, which was a simpler draw all/erase all design, was binned 
              	;		because I thought on real hardware it would create too much flash on the display.
              	;
              	;		I don't yet have a real machine so can't evaluate this in practice, as the emulator
              	; 		snapshots the display at 60Mhz, so the effects of endlessly messing with the 
              	;		CRAM and VRAM is largely hidden.
              	;
              	;		At 4Mhz it does about 330 draws/erases a second on a 16x16 sprites, twice as fast on
              	;		8 pixel high sprites.
              	;
              	;		It eats UDGs - a single 16x16 sprite needs 9 UDGs if it doesn't overlap with another.
              	;
              	; *********************************************************************************************
              	
              	
              	; *********************************************************************************************
              	;
              	;								  Sprite Record entries
              	;
              	; *********************************************************************************************
              	
0000:         	SPRx 	= 0 								; horizontal position, pixels
0002:         	SPRy 	= 2 								; vertical position, pixels
0004:         	SPRgraphics = 4 							; bitmap data
0006:         	SPRcontrol = 6 								; 0:width 1:height 5:HFlip 6:VFlip 7:hidden
              												; (others are zero)
0007:         	SPRstatus = 7 								; 7:currently drawn
              	
              	; *********************************************************************************************
              	;
              	; 								Initialise the sprite system.
              	;
              	; 	At this point sprite records should all have their "currently drawn" bit clear, it will
              	; 	get very confused otherwise.
              	;
              	; *********************************************************************************************
              	
0205:         	SPRInitialise:
0205: F5      			push 	af
0206: E5      			push 	hl
              			;
              			; 		Clear the main data area.
              			;
0207: 210005  			ld 		hl,SPRDataBlock 			
020A:         	_SPRZeroBlock:
020A: 3600    			ld 		(hl),$00
020C: 23      			inc 	hl
020D: 7C      			ld 		a,h
020E: FE09    			cp 		SPRDataBlockEnd >> 8
0210: 20F8    			jr 		nz,_SPRZeroBlock
              			;
              			; 		Set all possible original characters to $FF, indicating they are available.
              			;		
0212: 218005  			ld 		hl,SPROriginalChar+SPRLowSprite
0215:         	_SPRUsageReset:			 					
0215: 36FF    			ld 		(hl),$FF
0217: 2C      			inc 	l
0218: 20FB    			jr 		nz,_SPRUsageReset
021A: E1      			pop 	hl
021B: F1      			pop 	af
021C: C9      			ret
              	
              	; *********************************************************************************************
              	;
              	;						Draw, or Erase, the sprite whose raw data is at IX
              	;
              	; *********************************************************************************************
              	
021D:         	SpriteXDraw: 								; draw only
021D: DDCB077E			bit 	7,(ix+SPRstatus)
0221: C0      			ret 	nz
0222: 1805    			jr 		SpriteXToggle
0224:         	SpriteXErase:								; erase only
0224: DDCB077E			bit 	7,(ix+SPRstatus)
0228: C8      			ret 	z
0229:         	SpriteXToggle:								; flip state
0229: F5      			push 	af 							; save registers 							
022A: C5      			push 	bc
022B: D5      			push 	de
022C: E5      			push 	hl
022D: FDE5    			push 	iy
              			;
              			; 		Check actually visible
              			;
022F: DDCB067E			bit 	7,(ix+SPRcontrol)
0233: C27203  			jp 		nz,_SPRExit
              			;
              			; 		Check range.
              			;
0236: DD7E01  			ld 		a,(ix+SPRx+1) 				; MSB of X must be 0 or 1
0239: 47      			ld 		b,a 						; save in B
023A: E6FE    			and 	$FE
023C: DDB603  			or 		a,(ix+SPRy+1) 				; MSB of Y must be zero.
023F: 2011    			jr 		nz,_SPRRangeFail
              			;
0241: DD7E02  			ld 		a,(ix+SPRy) 				; check Y < 240
0244: FEF0    			cp 		8*30
0246: 300A    			jr 		nc,_SPRRangeFail
              			;
0248: 05      			dec 	b 							; if MSB X was 1, now zero
0249: 200A    			jr 		nz,_SPRCalcPosition 
              			;
024B: DD7E00  			ld 		a,(ix+SPRx) 				; X.MSB was 1, so must be X.LSB < 64
024E: FE40    			cp 		64
0250: 3803    			jr 		c,_SPRCalcPosition
0252:         	_SPRRangeFail:
0252: C37203  			jp 		_SPRExit 	
              			;
              			;		Calculate position in IY
              			;
0255:         	_SPRCalcPosition:		
0255: 2600    			ld 		h,0							; Y position in HL, with lower 3 bits masked, so already x 8
0257: DD7E02  			ld 		a,(ix+SPRy)
025A: E6F8    			and 	$F8
025C: 6F      			ld 		l,a
025D: 54      			ld 		d,h 						; DE = Y x 8
025E: 5D      			ld 		e,l
025F: 29      			add 	hl,hl 						; HL = Y x 32
0260: 29      			add 	hl,hl
0261: 19      			add 	hl,de 						; HL = Y x 40
0262: FD2100F8			ld 		iy,$F800 					; IY = $F800 + Y x 40
0266: EB      			ex 		de,hl
0267: FD19    			add 	iy,de
              	
0269: DD5E00  			ld 		e,(ix+SPRx)					; DE = X position
026C: DD5601  			ld 		d,(ix+SPRx+1)
026F: CB3A    			srl 	d 							; / 8 (after first in range 0-255 hence SRL E)
0271: CB1B    			rr 		e
0273: CB3B    			srl 	e
0275: CB3B    			srl 	e
0277: 1600    			ld 		d,0 						; add to screen position.
0279: FD19    			add 	iy,de
              			;
              			; 		Calculate and patch the fine horizontal shift jump which adjusts the 
              			; 		number of 24 bit left shifts we do to the graphics data.
              			;
027B: DD7E00  			ld 		a,(ix+SPRx)
027E: E607    			and 	7
0280: 87      			add 	a,a
0281: 321B03  			ld 		(_SPRFineHorizontalShift+1),a 
              			;
              			; 		Calculate the horizontal offset which makes it start drawing part way through a UDG
              			;
0284: DD7E02  			ld 		a,(ix+SPRy)
0287: E607    			and 	7
0289: 328104  			ld 		(_SPRInitialYOffset),a
              			;
              			; 		Calculate the row count from bit 1 of the control byte
              			; 		(the number of vertical pixels down)
              			;
028C: 3E08    			ld 		a,8
028E: DDCB064E			bit 	1,(ix+SPRcontrol)
0292: 2801    			jr 		z,_SPRSingleHeight
0294: 87      			add 	a,a
0295:         	_SPRSingleHeight:		
0295: 328004  			ld 		(_SPRRowCount),a
              			;
              			;		Set the sprite graphic address and incrementer.
              			;
0298: DD6E04  			ld 		l,(ix+SPRgraphics) 			; data address
029B: DD6605  			ld 		h,(ix+SPRgraphics+1) 		
029E: 110100  			ld 		de,1 						; increment/decrement
02A1: DDCB0646			bit 	0,(ix+SPRcontrol)
02A5: 2801    			jr 		z,_SPRSGANotDoubleWidth
02A7: 13      			inc 	de 							; 2 if double width
02A8:         	_SPRSGANotDoubleWidth:
02A8: DDCB0676			bit 	6,(ix+SPRcontrol) 			; check for vertical flip.
02AC: 2818    			jr 		z,_SPRSGANotVFlip
              			;
02AE: EB      			ex 		de,hl 						; DE = address, HL = increment x 8
02AF: E5      			push 	hl
02B0: 29      			add 	hl,hl
02B1: 29      			add 	hl,hl
02B2: 29      			add 	hl,hl
02B3: DDCB064E			bit 	1,(ix+SPRcontrol) 			; x 16 if double height
02B7: 2801    			jr 		z,_SPRSGANotDoubleHeight
02B9: 29      			add 	hl,hl		
02BA:         	_SPRSGANotDoubleHeight:
02BA: 19      			add 	hl,de 						; add 8/16 x increment to start
02BB: C1      			pop 	bc 							; original increment -> BC
02BC: E5      			push 	hl 							; save new start on stack.
02BD: 210000  			ld 		hl,0 						; HL = - increment
02C0: AF      			xor 	a
02C1: ED42    			sbc 	hl,bc
02C3: D1      			pop 	de 							; DE = new start off stack
02C4: EB      			ex 		de,hl 						; swap them back so HL = address, DE = -increment
02C5: 19      			add 	hl,de 						; points HL to the last sprite entry.
02C6:         	_SPRSGANotVFlip:
02C6: 22F902  			ld 		(_SPRFetchGraphicPtr+1),hl 	; write out start address in HL and incrementer in DE.		
02C9: ED530903			ld 		(_SPRAdjustGraphicPtr+1),de
              			;
              			; 		Try to allocate UDGs for the current row at IY, 2 or 3 UDGs.
              			;
02CD:         	_SPRStartNextCharacterRow:
02CD: DDCB077E			bit 	7,(ix+SPRstatus) 			; if it is not drawn skip next test
02D1: 2808    			jr 		z,_SPRSNCNoCheck
02D3: FD7E00  			ld 		a,(iy+0) 					; if we are erasing and have reached a non drawn graphic
02D6: FE80    			cp 		SPRLowSprite
02D8: D27203  			jp 		nc,_SPRExit 				; we can exit
              	
02DB:         	_SPRSNCNoCheck:		
02DB: CD8103  			call 	_SPRAllocateRow 			; try to allocate the whole row.
02DE: DA7203  			jp 		c,_SPRExit					; it didn't work, so we abandon drawing here.
              			;
              			; 		Adjust the usage counters.
              			;
02E1: FDE5    			push 	iy
02E3: CD2704  			call 	SPRAdjustUsageCounter
02E6: FD23    			inc 	iy
02E8: CD2704  			call 	SPRAdjustUsageCounter
02EB: DDCB0646			bit 	0,(ix+SPRcontrol)
02EF: 2805    			jr 		z,_SPRAuNotRight
02F1: FD23    			inc 	iy
02F3: CD2704  			call 	SPRAdjustUsageCounter
02F6:         	_SPRAuNotRight:
02F6: FDE1    			pop 	iy		
              			;
              			;		Get the graphics for the next *pixel* line. into ADE
              			;
02F8:         	_SPRNextRowUDG:		
              			;
02F8:         	_SPRFetchGraphicPtr:
02F8: 210000  			ld 		hl,$0000
02FB: 1E00    			ld 		e,0							; DE = $00:(HL)
02FD: 56      			ld 		d,(hl)
02FE: DDCB0646			bit 	0,(ix+SPRcontrol) 			; is the width 1 ?
0302: 2804    			jr 		z,_SPRHaveGraphicData
0304: 23      			inc 	hl
0305: 5A      			ld 		e,d  						; DE = (HL+1):(HL)		
0306: 56      			ld 		d,(hl)
0307: 2B      			dec 	hl		
0308:         	_SPRHaveGraphicData:		
              			;
0308:         	_SPRAdjustGraphicPtr:
0308: 010000  			ld 		bc,$0000 					; this is changed to account for size and
030B: 09      			add 	hl,bc 						; direction.
030C: 22F902  			ld 		(_SPRFetchGraphicPtr+1),hl		
030F: AF      			xor 	a 							; ADE contains 24 bit graphic data.
              			;
              			; 		Check for Horizontal Flip
              			;
0310: DDCB066E			bit 	5,(ix+SPRcontrol)			; if HFlip bit set
0314: 2803    			jr 		z,_SPRNoHFlip
0316: CD4304  			call 	SPRFlipADE 					; Flip ADE
0319:         	_SPRNoHFlip:		
0319: EB      			ex 		de,hl 						; we put it in AHL
031A:         	_SPRFineHorizontalShift:		
031A: 1800    			jr 		$+2 						; this is altered to do the fine horizontal shift
031C: 29      			add 	hl,hl
031D: 8F      			adc 	a,a
031E: 29      			add 	hl,hl
031F: 8F      			adc 	a,a
0320: 29      			add 	hl,hl
0321: 8F      			adc 	a,a
0322: 29      			add 	hl,hl
0323: 8F      			adc 	a,a
0324: 29      			add 	hl,hl
0325: 8F      			adc 	a,a
0326: 29      			add 	hl,hl
0327: 8F      			adc 	a,a
0328: 29      			add 	hl,hl
0329: 8F      			adc 	a,a
032A: 29      			add 	hl,hl
032B: 8F      			adc 	a,a
032C: EB      			ex 		de,hl 						; put it back in ADE
              			;
              			;		Now XOR the data with the previously calculated addresses. 
              			;		If (ix+5)[0] is clear then don't do the third one, it's an 8x8 sprite
              			;
              			;		These addresses (the ld hl,xxxx ones) are modified in situ.
              			;
032D:         	_SPRLeftUDGPosition:		
032D: 2108F6  			ld 		hl,$F000+$C1*8
0330: AE      			xor 	(hl)
0331: 77      			ld 		(hl),a
0332:         	_SPRMiddleUDGPosition:		
0332: 2110F6  			ld 		hl,$F000+$C2*8
0335: 7A      			ld 		a,d
0336: AE      			xor 	(hl)
0337: 77      			ld 		(hl),a
0338: DDCB0646			bit 	0,(ix+SPRcontrol) 					; if width 1, skip the last draw
033C: 2806    			jr 		z,_SPRDrawEnd
033E:         	_SPRRightUDGPosition:		
033E: 2118F6  			ld 		hl,$F000+$C3*8
0341: 7B      			ld 		a,e
0342: AE      			xor 	(hl)
0343: 77      			ld 		(hl),a
0344:         	_SPRDrawEnd:
              			;
              			; 		Check if we have done all the rows
              			;
0344: 218004  			ld 		hl,_SPRRowCount 
0347: 35      			dec 	(hl)
0348: 2828    			jr 		z,_SPRExit
              			;
              			; 		Now go to the next line down. Initially this just advances the vertical offset
              			;		in the UDG pointers
              			;
034A: 213303  			ld 		hl,_SPRMiddleUDGPosition+1
034D: 34      			inc 	(hl)
034E: 213F03  			ld 		hl,_SPRRightUDGPosition+1 	; not guaranteed initialised.
0351: 34      			inc 	(hl)
0352: 212E03  			ld 		hl,_SPRLeftUDGPosition+1
0355: 34      			inc 	(hl)
              			;
0356: 7E      			ld 		a,(hl) 						; check crossed 8 byte boundary
0357: E607    			and 	7
0359: 209D    			jr 		nz,_SPRNextRowUDG 			; if not complete it.
              	
035B: AF      			xor 	a 							; clear the initial offset
035C: 328104  			ld 		(_SPRInitialYOffset),a
              	
              	
035F: 112800  			ld 		de,40 						; advance down one row.
0362: FD19    			add 	iy,de 
              	
0364: 11B0FC  			ld 		de,$F800+40*30 				; the end of the physical display
0367: FDE5    			push 	iy
0369: E1      			pop 	hl
036A: 37      			scf
036B: ED52    			sbc 	hl,de
036D: 3003    			jr 		nc,_SPRExit 				; past the bottom,exit.
              	
036F: C3CD02  			jp 		_SPRStartNextCharacterRow 	; do the next character row.
              	
0372:         	_SPRExit:
0372: DD7E07  			ld 		a,(ix+SPRstatus) 			; toggle the drawn status bit
0375: EE80    			xor 	$80
0377: DD7707  			ld 		(ix+SPRstatus),a 		
              	
037A: FDE1    			pop 	iy 							; restore registers
037C: E1      			pop 	hl
037D: D1      			pop 	de
037E: C1      			pop 	bc
037F: F1      			pop 	af
0380: C9      			ret
              	
              	; *********************************************************************************************
              	;
              	;		Allocate 0-3 UDGs to the character space according to need and availability.
              	; 		Fail with CS if can't.
              	;		If possible,
              	;			all new UDGs should have the copied graphic from the background and the
              	;			old background set up.
              	;			the UDGs should replace the graphics in IY.
              	;
              	; *********************************************************************************************
              	
0381:         	_SPRAllocateRow:
0381: C5      			push 	bc 							; save BC.
0382: FDE5    			push 	iy 							; save IY
0384: ED738204			ld 		(_SPRAllocSPTemp),sp		; save SP as we are using it for temp.
              	
0388: 210000  			ld 		hl,$0000 					; we save all the allocated so far on the stack
038B: E5      			push 	hl 		 					; this is the end marker.					
              			;
              			; 		Do 2 or 3. For each overwrite the XOR code addresses and save
              			;		it on the stack. If it fails, then unwind everything.
              			;
038C: CDDC03  			call 	_SPRAllocateOne 			; do (IY)
038F: 3821    			jr 		c,_SPRAllocateUndo 			; if done, then Undo.
0391: 222E03  			ld 		(_SPRLeftUDGPosition+1),hl 	; overwrite the code.
0394: E5      			push 	hl
              	
0395: FD23    			inc 	iy		
0397: CDDC03  			call 	_SPRAllocateOne 			; do (IY+1)
039A: 3816    			jr 		c,_SPRAllocateUndo 			; if done, then Undo.
039C: 223303  			ld 		(_SPRMiddleUDGPosition+1),hl ; overwrite the code.
039F: E5      			push 	hl
              	
03A0: DDCB0646			bit 	0,(ix+SPRcontrol) 			; if 8 width then we are done.
03A4: 282D    			jr 		z,_SPRAllocateOkay 
              	
03A6: FD23    			inc 	iy		
03A8: CDDC03  			call 	_SPRAllocateOne 			; do (IY+2)
03AB: 3805    			jr 		c,_SPRAllocateUndo 			; if done, then Undo.
03AD: 223F03  			ld 		(_SPRRightUDGPosition+1),hl ; overwrite the code.
03B0: 1821    			jr 		_SPRAllocateOkay 
              			;
              			; 		Failed, so pop the saved UDG addresses on the stack and reset
              			;	 	as if we hadn't allocated it. We haven't bumped the usage count yet.
              			;
03B2:         	_SPRAllocateUndo:
03B2: D1      			pop 	de 							; address of UDG into DE
03B3: 7A      			ld 		a,d 						; have we done the whole lot ?
03B4: B3      			or 		e
03B5: 37      			scf
03B6: 281C    			jr 		z,_SPRAllocateExit 			; if so, e.g. popped $0000 with carry set.
              	
03B8: CB3A    			srl 	d 							; divide by 8 - will put the UDG number into E
03BA: CB1B    			rr 		e
03BC: CB3A    			srl 	d
03BE: CB1B    			rr 		e
03C0: CB3A    			srl 	d
03C2: CB1B    			rr 		e
              			;
03C4: 6B      			ld 		l,e 						; HL is the address of the original character for this UDG.
03C5: 2605    			ld 		h,SPROriginalChar >> 8 		
03C7: 7E      			ld 		a,(hl) 						; character the UDG replaced
03C8: 36FF    			ld 		(hl),$FF 					; mark that UDG as now available
              	
03CA: 2607    			ld 		h,SPRLowAddress >> 8 		; get screen address into DE
03CC: 5E      			ld 		e,(hl)
03CD: 2608    			ld 		h,SPRHighAddress >> 8 
03CF: 56      			ld 		d,(hl)
              	
03D0: 12      			ld 		(de),a 						; fix up screen
              	
03D1: 18DF    			jr 		_SPRAllocateUndo 			; and see if there are any more to undo
              			;
              			; 		Worked, exit with carry clear.
              			;
03D3:         	_SPRAllocateOkay: 							; clear carry flag and exit.
03D3: AF      			xor 	a		
03D4:         	_SPRAllocateExit:
03D4: ED7B8204			ld 		sp,(_SPRAllocSPTemp)		; get SP back
03D8: FDE1    			pop 	iy 							; restore BC IY
03DA: C1      			pop 	bc				
03DB: C9      			ret
              	
              	; *********************************************************************************************
              	;
              	; 		Allocate a single UDG sprite, overwriting (IY), saving the original and copying
              	; 		the definition. On exit HL points to its graphic definition.
              	;
              	; *********************************************************************************************
              	
03DC:         	_SPRAllocateOne:
03DC: FD7E00  			ld 		a,(iy+0) 					; is it a UDG already
03DF: FE80    			cp 		SPRLowSprite 				; if so, we don't need to do anything.
03E1: 3033    			jr 		nc,_SPRAllocateOneExit
              			;
              			; 		Look for a free UDG, e.g. one where the stored character is $FF.
              			;
03E3: 218005  			ld 		hl,SPROriginalChar+SPRLowSprite
03E6:         	_SPRAOFind: 								; look for an available UDG.
03E6: 7E      			ld 		a,(hl)
03E7: FEFF    			cp 		$FF
03E9: 2805    			jr 		z,_SPRAOFound
03EB: 2C      			inc 	l
03EC: 20F8    			jr 		nz,_SPRAOFind		
03EE: 37      			scf 								; nope, we just can't do this one.
03EF: C9      			ret
              	;
              	;  		Found a sprite we can allocate
              	;
03F0:         	_SPRAOFound:
              			;
              			; 		Store the character overwritten by the UDG
              			;
03F0: FD7E00  			ld 		a,(iy+0) 					; this is the original character e.g. what is underneath
03F3: 77      			ld 		(hl),a 						; put in storage slot for original character
              			;
03F4: FDE5    			push 	iy 							; save the address of that character so we can restore it.
03F6: C1      			pop 	bc 							; when it drops to zero.
03F7: 2607    			ld 		h,SPRLowAddress >> 8
03F9: 71      			ld 		(hl),c
03FA: 2608    			ld 		h,SPRHighAddress >> 8
03FC: 70      			ld 		(hl),b
              			;
              			; 		Copy the graphic definition of the original character into the UDG.
              			;
              	
03FD: FD7E00  			ld 		a,(iy+0) 					; get the original character , e.g. the non UDG
0400: FD7500  			ld 		(iy+0),l 					; override it.
              			;
0403: CD2004  			call 	_SPRCalculateDefinitionAddr ; HL is the graphic of the original character
0406: EB      			ex 		de,hl
0407: FD7E00  			ld 		a,(iy+0) 					; HL is the graphic of the UDG
040A: CD2004  			call 	_SPRCalculateDefinitionAddr
040D: EB      			ex 		de,hl 						; we want it copied there
040E: 010800  			ld 		bc,8 						; copy 8 bytes
0411: EDB0    			ldir
              	
0413: FD7E00  			ld 		a,(iy+0) 					; get the address of the UDG and exit with CC
0416:         	_SPRAllocateOneExit;
0416: CD2004  			call 	_SPRCalculateDefinitionAddr ; get the definition address in HL
0419: 3A8104  			ld 		a,(_SPRInitialYOffset) 		; adjust for initial Y offset
041C: B5      			or 		l
041D: 6F      			ld 		l,a
041E: AF      			xor 	a 							; clear carry.
041F: C9      			ret 			
              	;
              	; 		A is a character #, point HL to CRAM Address
              	;
0420:         	_SPRCalculateDefinitionAddr:
0420: 6F      			ld 		l,a
0421: 261E    			ld 		h,$F0/8
0423: 29      			add 	hl,hl
0424: 29      			add 	hl,hl
0425: 29      			add 	hl,hl
0426: C9      			ret
              	
              	; *********************************************************************************************
              	;
              	;							Adjust usage counter for (IY)
              	;
              	; *********************************************************************************************
              	
0427:         	SPRAdjustUsageCounter:
0427: FD6E00  			ld 		l,(iy+0) 					; point HL to the usage counter
042A: 2606    			ld 		h,SPRUsageCount >> 8
042C: DDCB077E			bit 	7,(ix+SPRstatus)			; if drawn status is non-zero we are erasing
0430: 2002    			jr 		nz,_SPRDecrementUsage
0432: 34      			inc 	(hl)						; increment usage counter and exit
0433: C9      			ret
              	;
0434:         	_SPRDecrementUsage:
0434: 35      			dec 	(hl) 						; one fewer usage
0435: C0      			ret 	nz 							; still in use.
              			;
              			; 		Count zero, free up. Could consider delaying this until actually needed?
              			;
0436: 2607    			ld 		h,SPRLowAddress >> 8 		; display address in DE
0438: 5E      			ld 		e,(hl)
0439: 2608    			ld 		h,SPRHighAddress >> 8
043B: 56      			ld 		d,(hl)
043C: 2605    			ld 		h,SPROriginalChar >> 8 		; original character written to DE
043E: 7E      			ld 		a,(hl)
043F: 12      			ld 		(de),a 					
              	
0440: 36FF    			ld 		(hl),$FF 					; mark the UDG as free again.
0442: C9      			ret
              	
              	
              	; *********************************************************************************************
              	;
              	;						Flip ADE - byteflip each and swap A and E
              	;
              	; *********************************************************************************************
              	
0443:         	SPRFlipADE:
0443: CD5304  			call 	_SPRFlipA 					; flip A
0446: F5      			push 	af
0447: 7A      			ld 	 	a,d 						; flip D
0448: CD5304  			call 	_SPRFlipA
044B: 57      			ld 		d,a
044C: 7B      			ld 		a,e 						; flip E -> A
044D: CD5304  			call 	_SPRFlipA
0450: E1      			pop 	hl 							; restore old A into E
0451: 5C      			ld 		e,h
0452: C9      			ret
              	;
              	; 		Flip A
              	;		
0453:         	_SPRFlipA:
0453: B7      			or 		a 							; shortcut, reverse zero.
0454: C8      			ret 	z
0455: CD5C04  			call 	_SPRFlipLow 				; flip the low nibble
0458: 0F      			rrca 								; swap halves
0459: 0F      			rrca
045A: 0F      			rrca
045B: 0F      			rrca 								; then fall through to flip high nibble.
045C:         	_SPRFlipLow:			
045C: F5      			push 	af 							; save original
045D: E60F    			and 	$0F 						; access the flip value.
045F: C670    			add 	_SPRFlipTable & $FF
0461: 6F      			ld 		l,a
0462: 2604    			ld 		h,_SPRFlipTable >> 8 
0464: F1      			pop 	af 							; restore original
0465: E6F0    			and 	$F0 						; replace lower nibble
0467: B6      			or 		(hl)
0468: C9      			ret
              	;
              	;		One Nibble Reversed.
              	;
0469: 00000000			.align 	16,0 						; all in one page.
046D: 000000  	
              	
0470:         	_SPRFlipTable:
0470: 0008040C			.db 	0,8,4,12,2,10,6,14
0474: 020A060E	
0478: 0109050D			.db 	1,9,5,13,3,11,7,15
047C: 030B070F	
              	
              	; *********************************************************************************************
              	;
              	; 									General Data
              	;
              	; *********************************************************************************************
              	
0480:         	_SPRRowCount: 								; down counter for completed rows.
0480: 00      			.db 	0
              	
0481:         	_SPRInitialYOffset: 						; the initial vertical offset.
0481: 00      			.db 	0
              	
0482:         	_SPRAllocSPTemp: 							; save SP when storing interim results on stack
0482: 0000    			.dw 	0
              	
              	; *********************************************************************************************
              	;
              	;		Sprite/UDG Specific Data. Each of these is a 256 byte array aligned
              	; 		on a page. This is a bit wasteful if you don't have many sprites but quicker.
              	;
              	; *********************************************************************************************
              	
0484: 00000000			.align 	256,0
0488: 00...   	
              	
0500:         	SPRDataBlock:
              	
              	;
              	;		This is the original value stored the UDG replaced. When $FF it means
              	; 		this UDG is not in use.
              	;
0500:         	SPROriginalChar:
0500: FFFFFFFF			.ds 	256
0504: FF...   	
              	;
              	;
              	; 		This is the number of sprites using the given UDG, indexed on zero.
              	;
0600:         	SPRUsageCount:
0600: FFFFFFFF			.ds 	256
0604: FF...   	
              	;
              	; 		The address of that replaced UDG. 
              	;
0700:         	SPRLowAddress:
0700: FFFFFFFF			.ds 	256
0704: FF...   	
0800:         	SPRHighAddress:
0800: FFFFFFFF			.ds 	256
0804: FF...   	
              	
0900:         	SPRDataBlockEnd:
              	
              	;
              	; 		Copy a lightweight pattern to the screen.
              	;
0900:         	start:
0900: 3100F0  			ld 		sp,$F000
0903: 21B0FC  			ld 		hl,$F800+40*30
0906: 2B      	_fill:	dec 	hl	
0907: 3620    	 		ld 		(hl),$20
0909: 7D      	 		ld 		a,l
090A: C602    	 		add 	a,2
090C: E603    	 		and 	3
090E: 2000    	 		jr 		nz,_fill3
              	; 		ld 		(hl),1
0910:         	_fill3: 		
0910: CB5C    	 		bit 	3,h
0912: 20F2    	 		jr 		nz,_fill
              	
0914: 2108F0  	 		ld 		hl,$F008
0917: 3601    	_fill4:	ld 		(hl),1
0919: 23      			inc 	hl
091A: CB5D    			bit 	3,l
091C: 20F9    			jr 		nz,_fill4 		
              	
091E: CD0502  	 		call 	SPRInitialise
0921: DD215009	 		ld 		ix,SpriteDemo
0925: CD3209  	 		call 	DoRow
0928: DD216009	 		ld 		ix,SpriteDemo2
092C: CD3209  	 		call 	DoRow
092F: F3      	_stop:	di
0930: 18FD    			jr		_stop 		
              	
0932:         	DoRow:
0932: 0608    	 		ld 		b,8
0934:         	_dloop: 		
0934: CD1D02  	 		call 	SpriteXDraw
0937: DD7E06  	 		ld 		a,(ix+6)
093A: C620    	 		add 	a,32
093C: E67F    	 		and 	$7F
093E: DD7706  	 		ld 		(ix+6),a
0941: DDCB07BE	 		res 	7,(ix+7)
              	
0945: DD7E00  	 		ld	 	a,(ix+0)
0948: C61C    	 		add 	a,28
094A: DD7700  	 		ld 		(ix+0),a
094D: 10E5    	 		djnz	_dloop
094F: C9      			ret		
              	
              	; *********************************************************************************************
              	;										Test data
              	; *********************************************************************************************
              	
0950:         	SpriteDemo:		
0950: 0400    			.dw 	4 							; X
0952: 0A00    			.dw 	10 							; Y
0954: A809    			.dw 	SpriteGraphic4 				; Graphics
0956: 0000    			.dw 	$00 						; Control
              	
0958:         	SpriteGraphic:
0958: FF818181			.db 	$FF,$81,$81,$81,$81,$81,$81,$FF
095C: 818181FF	
              	
0960:         	SpriteDemo2:		
0960: 1300    			.dw 	19 							; X
0962: 1C00    			.dw 	28 							; Y
0964: 8809    			.dw 	SpriteGraphic3	 			; Graphics
0966: 0300    			.dw 	$03							; Control
              	
0968:         	SpriteGraphic2:
0968: FFFF0180			.dw 	$FFFF,$8001,$F001,$8001,$8001,$8001,$8001,$AAAA
096C: 01F00180	
0970: 01800180	
0974: 0180AAAA	
0978: 555503C0			.dw 	$5555,$C003,$C003,$E007,$F00F,$F81F,$FC3F,$03C0
097C: 03C007E0	
0980: 0FF01FF8	
0984: 3FFCC003	
              	
0988:         	SpriteGraphic3:
0988: 00800040			.dw 	$8000,$4000,$2000,$1000,$0800,$0400,$0200,$0100
098C: 00200010	
0990: 00080004	
0994: 00020001	
0998: 80004000			.dw 	$0080,$0040,$0020,$0010,$0008,$000C,$000E,$000F
099C: 20001000	
09A0: 08000C00	
09A4: 0E000F00	
              	
09A8:         	SpriteGraphic4:
09A8: 80402010			.db 	$80,$40,$20,$10,8,12,14,15
09AC: 080C0E0F	


total time: 0.0033 sec.
no errors
