              	; --------------------------------------
              	; zasm: assemble "_demo.asm"
              	; date: 2021-11-01 07:25:33
              	; --------------------------------------


              	
              	
              	
0202:         			org 	$202
              	
              	;
              	;	 	Defining SPRLowSprite sets the lowest UDG used by sprites. This is a constant
              	; 		so if this is $A0, then $00-$9F are background, and $A0-$FF are used for sprites.
              	;
0080:         	SPRLowSprite = $80 								
              	
000A:         	SpriteCount = 10
              	
0202: C30008  			jp 		start
              			.include "xsprite.asm"
              	; *********************************************************************************************
              	; *********************************************************************************************
              	;
              	;		Name:		xsprite.asm
              	;		Purpose:	XOR Sprite Drawer
              	;		Created:	29th October 2021
              	;		Author:		Paul Robson (paul@robsons.org.uk)
              	;
              	; *********************************************************************************************
              	; *********************************************************************************************
              	
              	; *********************************************************************************************
              	;
              	;		This is an XOR drawing sprite system. This is useful because the draw is self
              	;		cancelling, and this means you don't have to repaint multiple sprites to restore
              	; 		the display state. The downside is that it doesn't work well on collisions or 
              	; 		especially backgrounds.
              	;
              	;		The original design, which was a simpler draw all/erase all design, was binned 
              	;		because I thought on real hardware it would create too much flash on the display.
              	;
              	;		I don't yet have a real machine so can't evaluate this in practice, as the emulator
              	; 		snapshots the display at 60Mhz, so the effects of endlessly messing with the 
              	;		CRAM and VRAM is largely hidden.
              	;
              	;		At 4Mhz it does about 330 draws/erases a second on a 16x16 sprites, twice as fast on
              	;		8 pixel high sprites.
              	;
              	;		It eats UDGs - a single 16x16 sprite needs 9 UDGs if it doesn't overlap with another.
              	;
              	; *********************************************************************************************
              	
              	
              	; *********************************************************************************************
              	;
              	;								  Sprite Record entries
              	;
              	; *********************************************************************************************
              	
0000:         	SPRx 	= 0 								; horizontal position, pixels
0002:         	SPRy 	= 2 								; vertical position, pixels
0004:         	SPRgraphics = 4 							; bitmap data
0006:         	SPRcontrol = 6 								; 0:width 1:height 7:hidden
              												; (others are zero)
0007:         	SPRstatus = 7 								; 7:currently drawn
              	
              	; *********************************************************************************************
              	;
              	; 								Initialise the sprite system.
              	;
              	; 	At this point sprite records should all have their "currently drawn" bit clear, it will
              	; 	get very confused otherwise.
              	;
              	; *********************************************************************************************
              	
0205:         	SPRInitialise:
0205: F5      			push 	af
0206: E5      			push 	hl
              			;
              			; 		Clear the main data area.
              			;
0207: 210004  			ld 		hl,SPRDataBlock 			
020A:         	_SPRZeroBlock:
020A: 3600    			ld 		(hl),$00
020C: 23      			inc 	hl
020D: 7C      			ld 		a,h
020E: FE08    			cp 		SPRDataBlockEnd >> 8
0210: 20F8    			jr 		nz,_SPRZeroBlock
              			;
              			; 		Set all possible original characters to $FF, indicating they are available.
              			;		
0212: 218004  			ld 		hl,SPROriginalChar+SPRLowSprite
0215:         	_SPRUsageReset:			 					
0215: 36FF    			ld 		(hl),$FF
0217: 2C      			inc 	l
0218: 20FB    			jr 		nz,_SPRUsageReset
021A: E1      			pop 	hl
021B: F1      			pop 	af
021C: C9      			ret
              	
              	; *********************************************************************************************
              	;
              	;						Draw, or Erase, the sprite whose raw data is at IX
              	;
              	; *********************************************************************************************
              	
021D:         	SpriteXDraw: 								; draw only
021D: DDCB077E			bit 	7,(ix+SPRstatus)
0221: C0      			ret 	nz
0222: 1805    			jr 		SpriteXToggle
0224:         	SpriteXErase:								; erase only
0224: DDCB077E			bit 	7,(ix+SPRstatus)
0228: C8      			ret 	z
0229:         	SpriteXToggle:								; flip state
0229: F5      			push 	af 							; save registers 							
022A: C5      			push 	bc
022B: D5      			push 	de
022C: E5      			push 	hl
022D: FDE5    			push 	iy
              			;
              			; 		Check actually visible
              			;
022F: DDCB067E			bit 	7,(ix+SPRcontrol)
0233: C22403  			jp 		nz,_SPRExit
              			;
              			; 		Check range.
              			;
0236: DD7E01  			ld 		a,(ix+SPRx+1) 				; MSB of X must be 0 or 1
0239: 47      			ld 		b,a 						; save in B
023A: E6FE    			and 	$FE
023C: DDB603  			or 		a,(ix+SPRy+1) 				; MSB of Y must be zero.
023F: 2011    			jr 		nz,_SPRRangeFail
              			;
0241: DD7E02  			ld 		a,(ix+SPRy) 				; check Y < 240
0244: FEF0    			cp 		8*30
0246: 300A    			jr 		nc,_SPRRangeFail
              			;
0248: 05      			dec 	b 							; if MSB X was 1, now zero
0249: 200A    			jr 		nz,_SPRCalcPosition 
              			;
024B: DD7E00  			ld 		a,(ix+SPRx) 				; X.MSB was 1, so must be X.LSB < 64
024E: FE40    			cp 		64
0250: 3803    			jr 		c,_SPRCalcPosition
0252:         	_SPRRangeFail:
0252: C32403  			jp 		_SPRExit 	
              			;
              			;		Calculate position in IY
              			;
0255:         	_SPRCalcPosition:		
0255: 2600    			ld 		h,0							; Y position in HL, with lower 3 bits masked, so already x 8
0257: DD7E02  			ld 		a,(ix+SPRy)
025A: E6F8    			and 	$F8
025C: 6F      			ld 		l,a
025D: 54      			ld 		d,h 						; DE = Y x 8
025E: 5D      			ld 		e,l
025F: 29      			add 	hl,hl 						; HL = Y x 32
0260: 29      			add 	hl,hl
0261: 19      			add 	hl,de 						; HL = Y x 40
0262: FD2100F8			ld 		iy,$F800 					; IY = $F800 + Y x 40
0266: EB      			ex 		de,hl
0267: FD19    			add 	iy,de
              	
0269: DD5E00  			ld 		e,(ix+SPRx)					; DE = X position
026C: DD5601  			ld 		d,(ix+SPRx+1)
026F: CB3A    			srl 	d 							; / 8 (after first in range 0-255 hence SRL E)
0271: CB1B    			rr 		e
0273: CB3B    			srl 	e
0275: CB3B    			srl 	e
0277: 1600    			ld 		d,0 						; add to screen position.
0279: FD19    			add 	iy,de
              			;
              			; 		Calculate and patch the fine horizontal shift jump which adjusts the 
              			; 		number of 24 bit left shifts we do to the graphics data.
              			;
027B: DD7E00  			ld 		a,(ix+SPRx)
027E: E607    			and 	7
0280: 87      			add 	a,a
0281: 32CD02  			ld 		(_SPRFineHorizontalShift+1),a 
              			;
              			; 		Calculate the horizontal offset which makes it start drawing part way through a UDG
              			;
0284: DD7E02  			ld 		a,(ix+SPRy)
0287: E607    			and 	7
0289: 32F603  			ld 		(_SPRInitialYOffset),a
              			;
              			; 		Calculate the row count from bit 1 of the control byte
              			; 		(the number of vertical pixels down)
              			;
028C: 3E08    			ld 		a,8
028E: DDCB064E			bit 	1,(ix+SPRcontrol)
0292: 2801    			jr 		z,_SPRSingleHeight
0294: 87      			add 	a,a
0295:         	_SPRSingleHeight:		
0295: 32F503  			ld 		(_SPRRowCount),a
              			;
              			; 		Load BC with the sprite graphic data, we preserve this throughout
              			; 		drawing.
              			;
0298: DD4E04  			ld 		c,(ix+SPRgraphics)
029B: DD4605  			ld 		b,(ix+SPRgraphics+1)
              			;
              			; 		Try to allocate UDGs for the current row at IY, 2 or 3 UDGs.
              			;
029E:         	_SPRStartNextCharacterRow:
029E: CD3303  			call 	_SPRAllocateRow 			; try to allocate the whole row.
02A1: DA2403  			jp 		c,_SPRExit					; it didn't work, so we abandon drawing here.
              			;
              			; 		Adjust the usage counters.
              			;
02A4: FDE5    			push 	iy
02A6: CDD903  			call 	SPRAdjustUsageCounter
02A9: FD23    			inc 	iy
02AB: CDD903  			call 	SPRAdjustUsageCounter
02AE: DDCB0646			bit 	0,(ix+SPRcontrol)
02B2: 2805    			jr 		z,_SPRAuNotRight
02B4: FD23    			inc 	iy
02B6: CDD903  			call 	SPRAdjustUsageCounter
02B9:         	_SPRAuNotRight:
02B9: FDE1    			pop 	iy		
              			;
              			;		Get the graphics for the next *pixel* line. into ADE
              			;
02BB:         	_SPRNextRowUDG:		
02BB: 1E00    			ld 		e,0							; DE = $00:BC
02BD: 0A      			ld 		a,(bc)
02BE: 57      			ld 		d,a
02BF: 03      			inc 	bc
02C0: DDCB0646			bit 	0,(ix+SPRcontrol) 					; is the width 1 ?
02C4: 2804    			jr 		z,_SPRHaveGraphicData
02C6: 5A      			ld 		e,d  						; DE = (BC+1):(BC)		
02C7: 0A      			ld 		a,(bc)
02C8: 57      			ld 		d,a 
02C9: 03      			inc 	bc
02CA:         	_SPRHaveGraphicData:		
02CA: AF      			xor 	a 							; ADE contains 24 bit graphic data.
02CB: EB      			ex 		de,hl 						; we put it in AHL
02CC:         	_SPRFineHorizontalShift:		
02CC: 1800    			jr 		$+2 						; this is altered to do the fine horizontal shift
02CE: 29      			add 	hl,hl
02CF: 8F      			adc 	a,a
02D0: 29      			add 	hl,hl
02D1: 8F      			adc 	a,a
02D2: 29      			add 	hl,hl
02D3: 8F      			adc 	a,a
02D4: 29      			add 	hl,hl
02D5: 8F      			adc 	a,a
02D6: 29      			add 	hl,hl
02D7: 8F      			adc 	a,a
02D8: 29      			add 	hl,hl
02D9: 8F      			adc 	a,a
02DA: 29      			add 	hl,hl
02DB: 8F      			adc 	a,a
02DC: 29      			add 	hl,hl
02DD: 8F      			adc 	a,a
02DE: EB      			ex 		de,hl 						; put it back in ADE
              			;
              			;		Now XOR the data with the previously calculated addresses. 
              			;		If (ix+5)[0] is clear then don't do the third one, it's an 8x8 sprite
              			;
              			;		These addresses (the ld hl,xxxx ones) are modified in situ.
              			;
02DF:         	_SPRLeftUDGPosition:		
02DF: 2108F6  			ld 		hl,$F000+$C1*8
02E2: AE      			xor 	(hl)
02E3: 77      			ld 		(hl),a
02E4:         	_SPRMiddleUDGPosition:		
02E4: 2110F6  			ld 		hl,$F000+$C2*8
02E7: 7A      			ld 		a,d
02E8: AE      			xor 	(hl)
02E9: 77      			ld 		(hl),a
02EA: DDCB0646			bit 	0,(ix+SPRcontrol) 					; if width 1, skip the last draw
02EE: 2806    			jr 		z,_SPRDrawEnd
02F0:         	_SPRRightUDGPosition:		
02F0: 2118F6  			ld 		hl,$F000+$C3*8
02F3: 7B      			ld 		a,e
02F4: AE      			xor 	(hl)
02F5: 77      			ld 		(hl),a
02F6:         	_SPRDrawEnd:
              			;
              			; 		Check if we have done all the rows
              			;
02F6: 21F503  			ld 		hl,_SPRRowCount 
02F9: 35      			dec 	(hl)
02FA: 2828    			jr 		z,_SPRExit
              			;
              			; 		Now go to the next line down. Initially this just advances the vertical offset
              			;		in the UDG pointers
              			;
02FC: 21E502  			ld 		hl,_SPRMiddleUDGPosition+1
02FF: 34      			inc 	(hl)
0300: 21F102  			ld 		hl,_SPRRightUDGPosition+1 	; not guaranteed initialised.
0303: 34      			inc 	(hl)
0304: 21E002  			ld 		hl,_SPRLeftUDGPosition+1
0307: 34      			inc 	(hl)
              			;
0308: 7E      			ld 		a,(hl) 						; check crossed 8 byte boundary
0309: E607    			and 	7
030B: 20AE    			jr 		nz,_SPRNextRowUDG 			; if not complete it.
              	
030D: AF      			xor 	a 							; clear the initial offset
030E: 32F603  			ld 		(_SPRInitialYOffset),a
              	
              	
0311: 112800  			ld 		de,40 						; advance down one row.
0314: FD19    			add 	iy,de 
              	
0316: 11B0FC  			ld 		de,$F800+40*30 				; the end of the physical display
0319: FDE5    			push 	iy
031B: E1      			pop 	hl
031C: 37      			scf
031D: ED52    			sbc 	hl,de
031F: 3003    			jr 		nc,_SPRExit 				; past the bottom,exit.
              	
0321: C39E02  			jp 		_SPRStartNextCharacterRow 	; do the next character row.
              	
0324:         	_SPRExit:
0324: DD7E07  			ld 		a,(ix+SPRstatus) 			; toggle the drawn status bit
0327: EE80    			xor 	$80
0329: DD7707  			ld 		(ix+SPRstatus),a 		
              	
032C: FDE1    			pop 	iy 							; restore registers
032E: E1      			pop 	hl
032F: D1      			pop 	de
0330: C1      			pop 	bc
0331: F1      			pop 	af
0332: C9      			ret
              	
              	; *********************************************************************************************
              	;
              	;		Allocate 0-3 UDGs to the character space according to need and availability.
              	; 		Fail with CS if can't.
              	;		If possible,
              	;			all new UDGs should have the copied graphic from the background and the
              	;			old background set up.
              	;			the UDGs should replace the graphics in IY.
              	;
              	; *********************************************************************************************
              	
0333:         	_SPRAllocateRow:
0333: C5      			push 	bc 							; save BC.
0334: FDE5    			push 	iy 							; save IY
0336: ED73F703			ld 		(_SPRAllocSPTemp),sp		; save SP as we are using it for temp.
              	
033A: 210000  			ld 		hl,$0000 					; we save all the allocated so far on the stack
033D: E5      			push 	hl 		 					; this is the end marker.					
              			;
              			; 		Do 2 or 3. For each overwrite the XOR code addresses and save
              			;		it on the stack. If it fails, then unwind everything.
              			;
033E: CD8E03  			call 	_SPRAllocateOne 			; do (IY)
0341: 3821    			jr 		c,_SPRAllocateUndo 			; if done, then Undo.
0343: 22E002  			ld 		(_SPRLeftUDGPosition+1),hl 	; overwrite the code.
0346: E5      			push 	hl
              	
0347: FD23    			inc 	iy		
0349: CD8E03  			call 	_SPRAllocateOne 			; do (IY+1)
034C: 3816    			jr 		c,_SPRAllocateUndo 			; if done, then Undo.
034E: 22E502  			ld 		(_SPRMiddleUDGPosition+1),hl ; overwrite the code.
0351: E5      			push 	hl
              	
0352: DDCB0646			bit 	0,(ix+SPRcontrol) 			; if 8 width then we are done.
0356: 282D    			jr 		z,_SPRAllocateOkay 
              	
0358: FD23    			inc 	iy		
035A: CD8E03  			call 	_SPRAllocateOne 			; do (IY+2)
035D: 3805    			jr 		c,_SPRAllocateUndo 			; if done, then Undo.
035F: 22F102  			ld 		(_SPRRightUDGPosition+1),hl ; overwrite the code.
0362: 1821    			jr 		_SPRAllocateOkay 
              			;
              			; 		Failed, so pop the saved UDG addresses on the stack and reset
              			;	 	as if we hadn't allocated it. We haven't bumped the usage count yet.
              			;
0364:         	_SPRAllocateUndo:
0364: D1      			pop 	de 							; address of UDG into DE
0365: 7A      			ld 		a,d 						; have we done the whole lot ?
0366: B3      			or 		e
0367: 37      			scf
0368: 281C    			jr 		z,_SPRAllocateExit 			; if so, e.g. popped $0000 with carry set.
              	
036A: CB3A    			srl 	d 							; divide by 8 - will put the UDG number into E
036C: CB1B    			rr 		e
036E: CB3A    			srl 	d
0370: CB1B    			rr 		e
0372: CB3A    			srl 	d
0374: CB1B    			rr 		e
              			;
0376: 6F      			ld 		l,a 						; HL is the address of the original character for this UDG.
0377: 2604    			ld 		h,SPROriginalChar >> 8 		
0379: 7E      			ld 		a,(hl) 						; character the UDG replaced
037A: 36FF    			ld 		(hl),$FF 					; mark that UDG as now available
              	
037C: 2606    			ld 		h,SPRLowAddress >> 8 		; get screen address into DE
037E: 5E      			ld 		e,(hl)
037F: 2607    			ld 		h,SPRHighAddress >> 8 
0381: 56      			ld 		d,(hl)
              	
0382: 12      			ld 		(de),a 						; fix up screen
              	
0383: 18DF    			jr 		_SPRAllocateUndo 			; and see if there are any more to undo
              			;
              			; 		Worked, exit with carry clear.
              			;
0385:         	_SPRAllocateOkay: 							; clear carry flag and exit.
0385: AF      			xor 	a		
0386:         	_SPRAllocateExit:
0386: ED7BF703			ld 		sp,(_SPRAllocSPTemp)		; get SP back
038A: FDE1    			pop 	iy 							; restore BC IY
038C: C1      			pop 	bc				
038D: C9      			ret
              	
              	; *********************************************************************************************
              	;
              	; 		Allocate a single UDG sprite, overwriting (IY), saving the original and copying
              	; 		the definition. On exit HL points to its graphic definition.
              	;
              	; *********************************************************************************************
              	
038E:         	_SPRAllocateOne:
038E: FD7E00  			ld 		a,(iy+0) 					; is it a UDG already
0391: FE80    			cp 		SPRLowSprite 				; if so, we don't need to do anything.
0393: 3033    			jr 		nc,_SPRAllocateOneExit
              			;
              			; 		Look for a free UDG, e.g. one where the stored character is $FF.
              			;
0395: 218004  			ld 		hl,SPROriginalChar+SPRLowSprite
0398:         	_SPRAOFind: 								; look for an available UDG.
0398: 7E      			ld 		a,(hl)
0399: FEFF    			cp 		$FF
039B: 2805    			jr 		z,_SPRAOFound
039D: 2C      			inc 	l
039E: 20F8    			jr 		nz,_SPRAOFind		
03A0: 37      			scf 								; nope, we just can't do this one.
03A1: C9      			ret
              	;
              	;  		Found a sprite we can allocate
              	;
03A2:         	_SPRAOFound:
              			;
              			; 		Store the character overwritten by the UDG
              			;
03A2: FD7E00  			ld 		a,(iy+0) 					; this is the original character e.g. what is underneath
03A5: 77      			ld 		(hl),a 						; put in storage slot for original character
              			;
03A6: FDE5    			push 	iy 							; save the address of that character so we can restore it.
03A8: C1      			pop 	bc 							; when it drops to zero.
03A9: 2606    			ld 		h,SPRLowAddress >> 8
03AB: 71      			ld 		(hl),c
03AC: 2607    			ld 		h,SPRHighAddress >> 8
03AE: 70      			ld 		(hl),b
              			;
              			; 		Copy the graphic definition of the original character into the UDG.
              			;
              	
03AF: FD7E00  			ld 		a,(iy+0) 					; get the original character , e.g. the non UDG
03B2: FD7500  			ld 		(iy+0),l 					; override it.
              			;
03B5: CDD203  			call 	_SPRCalculateDefinitionAddr ; HL is the graphic of the original character
03B8: EB      			ex 		de,hl
03B9: FD7E00  			ld 		a,(iy+0) 					; HL is the graphic of the UDG
03BC: CDD203  			call 	_SPRCalculateDefinitionAddr
03BF: EB      			ex 		de,hl 						; we want it copied there
03C0: 010800  			ld 		bc,8 						; copy 8 bytes
03C3: EDB0    			ldir
              	
03C5: FD7E00  			ld 		a,(iy+0) 					; get the address of the UDG and exit with CC
03C8:         	_SPRAllocateOneExit;
03C8: CDD203  			call 	_SPRCalculateDefinitionAddr ; get the definition address in HL
03CB: 3AF603  			ld 		a,(_SPRInitialYOffset) 		; adjust for initial Y offset
03CE: B5      			or 		l
03CF: 6F      			ld 		l,a
03D0: AF      			xor 	a 							; clear carry.
03D1: C9      			ret 			
              	;
              	; 		A is a character #, point HL to CRAM Address
              	;
03D2:         	_SPRCalculateDefinitionAddr:
03D2: 6F      			ld 		l,a
03D3: 261E    			ld 		h,$F0/8
03D5: 29      			add 	hl,hl
03D6: 29      			add 	hl,hl
03D7: 29      			add 	hl,hl
03D8: C9      			ret
              	
              	; *********************************************************************************************
              	;
              	;							Adjust usage counter for (IY)
              	;
              	; *********************************************************************************************
              	
03D9:         	SPRAdjustUsageCounter:
03D9: FD6E00  			ld 		l,(iy+0) 					; point HL to the usage counter
03DC: 2605    			ld 		h,SPRUsageCount >> 8
03DE: DDCB077E			bit 	7,(ix+SPRstatus)			; if drawn status is non-zero we are erasing
03E2: 2002    			jr 		nz,_SPRDecrementUsage
03E4: 34      			inc 	(hl)						; increment usage counter and exit
03E5: C9      			ret
              	;
03E6:         	_SPRDecrementUsage:
03E6: 35      			dec 	(hl) 						; one fewer usage
03E7: C0      			ret 	nz 							; still in use.
              			;
              			; 		Count zero, free up. Could consider delaying this until actually needed?
              			;
03E8: 2606    			ld 		h,SPRLowAddress >> 8 		; display address in DE
03EA: 5E      			ld 		e,(hl)
03EB: 2607    			ld 		h,SPRHighAddress >> 8
03ED: 56      			ld 		d,(hl)
03EE: 2604    			ld 		h,SPROriginalChar >> 8 		; original character written to DE
03F0: 7E      			ld 		a,(hl)
03F1: 12      			ld 		(de),a 					
              	
03F2: 36FF    			ld 		(hl),$FF 					; mark the UDG as free again.
03F4: C9      			ret
              	
              	; *********************************************************************************************
              	;
              	; 									General Data
              	;
              	; *********************************************************************************************
              	
03F5:         	_SPRRowCount: 								; down counter for completed rows.
03F5: 00      			.db 	0
              	
03F6:         	_SPRInitialYOffset: 						; the initial vertical offset.
03F6: 00      			.db 	0
              	
03F7:         	_SPRAllocSPTemp: 							; save SP when storing interim results on stack
03F7: 0000    			.dw 	0
              	
              	; *********************************************************************************************
              	;
              	;		Sprite/UDG Specific Data. Each of these is a 256 byte array aligned
              	; 		on a page. This is a bit wasteful if you don't have many sprites but quicker.
              	;
              	; *********************************************************************************************
              	
03F9: 00000000			.align 	256,0
03FD: 000000  	
              	
0400:         	SPRDataBlock:
              	
              	;
              	;		This is the original value stored the UDG replaced. When $FF it means
              	; 		this UDG is not in use.
              	;
0400:         	SPROriginalChar:
0400: FFFFFFFF			.ds 	256
0404: FF...   	
              	;
              	;
              	; 		This is the number of sprites using the given UDG, indexed on zero.
              	;
0500:         	SPRUsageCount:
0500: FFFFFFFF			.ds 	256
0504: FF...   	
              	;
              	; 		The address of that replaced UDG. 
              	;
0600:         	SPRLowAddress:
0600: FFFFFFFF			.ds 	256
0604: FF...   	
0700:         	SPRHighAddress:
0700: FFFFFFFF			.ds 	256
0704: FF...   	
              	
0800:         	SPRDataBlockEnd:
              	
              	
              	;
              	; 		Copy a lightweight pattern to the screen.
              	;
0800:         	start:
0800: 3100F0  			ld 		sp,$F000
0803: 21B0FC  			ld 		hl,$F800+40*30
0806: 2B      	_fill:	dec 	hl	
0807: 3620    	 		ld 		(hl),$20
0809: 7D      	 		ld 		a,l
080A: C602    	 		add 	a,2
080C: E603    	 		and 	3
080E: 2002    	 		jr 		nz,_fill3
0810: 3601    	 		ld 		(hl),1
0812:         	_fill3: 		
0812: CB5C    	 		bit 	3,h
0814: 20F0    	 		jr 		nz,_fill
              	
0816: 2108F0  	 		ld 		hl,$F008
0819: 3601    	_fill4:	ld 		(hl),1
081B: 23      			inc 	hl
081C: CB5D    			bit 	3,l
081E: 20F9    			jr 		nz,_fill4 		
              	
0820: CD0502  	 		call 	SPRInitialise
              	
0823: DD21C108	 		ld 		ix,SpriteBuffer
0827: 060A    	 		ld 		b,SpriteCount
0829:         	_create:
0829: 78      			ld 		a,b
082A: 87      			add 	a,a
082B: 87      			add 	a,a		
082C: 87      			add 	a,a
082D: 87      			add 	a,a
082E: C618    			add 	a,24
0830: DD7700  			ld 		(ix+0),a
0833: DD360100			ld 		(ix+1),0
0837: 87      			add 	a,a
0838: DD7702  			ld 		(ix+2),a
083B: DD360300			ld 		(ix+3),0
083F: DD360489			ld 		(ix+4),SpriteGraphic & 0xFF
0843: DD360509			ld 		(ix+5),SpriteGraphic >> 8
0847: DD360603			ld 		(ix+6),$3
084B: DD360700			ld 		(ix+7),0
084F: DD360801			ld 		(ix+8),1
0853: CB40    			bit 	0,b
0855: 2804    			jr 		z,_create1
0857: DD3608FF			ld 		(ix+8),255
085B:         	_create1:		
085B: DD360901			ld 		(ix+9),1
085F: 110A00  	 		ld 		de,10
0862: DD19    	 		add 	ix,de
0864: 10C3    	 		djnz 	_create
              	
0866: 210000  	 		ld 		hl,0
0869: 220001  	 		ld 		($0100),hl
              	
086C: DD21C108	_loop1:	ld 		ix,SpriteBuffer
0870: 060A    			ld 		b,SpriteCount
0872: CD2402  	_loop2:	call 	SpriteXErase
0875: CD8F08  			call 	moveOne
0878: CD1D02  			call 	SpriteXDraw
              	
087B: 2A0001  			ld 		hl,($0100)
087E: 23      			inc 	hl
087F: 23      			inc 	hl
0880: 220001  	 		ld 		($0100),hl
              	
0883: 110A00  	 		ld 		de,10
0886: DD19    	 		add 	ix,de
0888: 10E8    			djnz 	_loop2
088A: 18E0    			jr 		_loop1
              	
088C: F3      	_stop:	di
088D: 18FD    			jr		_stop 		
              	
088F:         	moveOne:
088F: DD7E08  			ld 		a,(ix+8)
0892: CDAA08  			call 	advance
0895: DD7708  			ld 		(ix+8),a
0898: DD7E09  			ld 		a,(ix+9)
089B: DDE5    			push 	ix
089D: DD23    			inc 	ix
089F: DD23    			inc 	ix
08A1: CDAA08  			call 	advance
08A4: DDE1    			pop 	ix		
08A6: DD7709  			ld 		(ix+9),a
08A9: C9      			ret
              	
08AA: 4F      	advance:ld 		c,a
08AB: 87      			add 	a,a
08AC: 87      			add 	a,a
08AD: DD8600  			add 	a,(ix+0)
08B0: DD7700  			ld 		(ix+0),a
08B3: FE10    			cp 		16
08B5: 3806    			jr 		c,_adv2
08B7: FEF0    			cp 		240
08B9: 3002    			jr 		nc,_adv2
08BB: 79      			ld 		a,c
08BC: C9      			ret
08BD:         	_adv2:
08BD: 79      			ld 		a,c
08BE: ED44    			neg
08C0: C9      			ret
              	
              	; *********************************************************************************************
              	;										Test data
              	; *********************************************************************************************
              	
08C1:         	SpriteBuffer:		
08C1: FFFFFFFF			.ds 	SpriteCount * 20
08C5: FF...   	
              	
              	
0989:         	SpriteGraphic:
0989: F00F0810			.dw 	$0FF0,$1008,$2004,$4002,$8001,$FFFF,$AAAB,$D555
098D: 04200240	
0991: 0180FFFF	
0995: ABAA55D5	
0999: 03C003C0			.dw 	$C003,$C003,$FFFF,$8001,$4002,$2004,$1008,$0FF0
099D: FFFF0180	
09A1: 02400420	
09A5: 0810F00F	
              	


total time: 0.0030 sec.
no errors
